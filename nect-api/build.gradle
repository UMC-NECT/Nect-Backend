plugins {
	id 'org.asciidoctor.jvm.convert' version '4.0.1'
	id 'com.epages.restdocs-api-spec' version '0.18.2'
}

dependencies {

	// Internal modules
	implementation project(':nect-core')
	implementation project(':nect-client')

	// Security
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	// Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2', 'io.jsonwebtoken:jjwt-jackson:0.11.2'

	// Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// Logbook
	implementation 'org.zalando:logbook-spring-boot-starter:3.9.0'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// REST Docs
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.2'

	// JSON Schema generator
	implementation 'com.github.victools:jsonschema-generator:4.36.0'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// S3
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
  
	// WebSocket
	implementation 'org.springframework.boot:spring-boot-starter-websocket'

}

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	useJUnitPlatform()
	outputs.dir snippetsDir
}

tasks.withType(Test).configureEach {
    maxHeapSize = "2g"
    jvmArgs(
            "-XX:+HeapDumpOnOutOfMemoryError",
            "-XX:HeapDumpPath=${rootProject.projectDir}/build/heapdumps"
    )

    doFirst {
        file("${rootProject.projectDir}/build/heapdumps").mkdirs()
    }
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

openapi3 {
	// QUESTION : 500번대 포트연결 에러가 뜨길래 넣어서 해결했는데 넣어야 하는지 궁금합니다.
	servers = [
		{ url = 'http://localhost:8080' },
		{ url = 'https://getnect.tech' }
	]
	title = 'Nect API Document'
	description = 'Nect Application API Document'
	version = '0.0.1'
	format = 'yaml'
}

tasks.register('copyOasToSwagger', Copy) {
	delete 'src/main/resources/static/swagger-ui/openapi3.yaml'
	from "${layout.buildDirectory.get()}/api-spec/openapi3.yaml"
	into "${layout.buildDirectory.get()}/resources/main/static/swagger-ui/."
	dependsOn 'openapi3'
}

tasks.resolveMainClassName {
	dependsOn 'copyOasToSwagger'
}

bootJar {
	enabled = true
}

jar {
	enabled = false
}
